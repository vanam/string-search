plugins {
    id 'java-library'
    id 'jacoco'
    id 'checkstyle'
    id 'ru.vyarus.quality' version '3.4.0'
    id 'maven-publish'
    id 'signing'
}

group 'com.martinvana'
version '1.0.0'

sourceCompatibility = 1.11

repositories {
    mavenCentral()
}

quality {
    checkstyleVersion = '8.26'
    autoRegistration = false
    checkstyle = true
    configDir = 'config/'
}

ext {
    junitVersion = '5.5.2'
}

dependencies {
    testImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
    testCompile "org.junit.jupiter:junit-jupiter-params:$junitVersion"
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled false
    }
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/vanam/string-search")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("PASSWORD")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}

check.dependsOn jacocoTestReport

